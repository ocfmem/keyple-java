def version = "0.8.0"

if(project.getProperties().containsKey("release") && release=="true"){
    project.version = version
}else{
    project.version = version+'-'+ timestamp+ '-SNAPSHOT'
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
archivesBaseName = "keyple-android-plugin-omapi"

group 'org.eclipse.keyple'
project.description = 'Keyple Plugin Android OMAPI'

pmd {
    ruleSets = [
            "java-basic",
            "java-braces",
    ]
}

android {
    compileSdkVersion 26

    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 21
        versionName project.version
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    buildTypes {
        release {
            minifyEnabled false
        }
    }
    
    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_6
        targetCompatibility JavaVersion.VERSION_1_6
    }

    testOptions {
        unitTests.returnDefaultValues = true//mock Log Android object
    }

    buildToolsVersion '26.0.2'

    lintOptions {
        abortOnError false
    }

    //generate output aar with a qualified name : with version number
    libraryVariants.all { variant ->
        variant.outputs.all { output ->
            if (outputFile != null && outputFileName.endsWith('.aar')) {
                outputFileName = "${archivesBaseName}-${version}.aar"
            }
        }
    }

    //create a task to generate javadoc for each variants
    libraryVariants.all { variant ->
        task("generate${variant.name.capitalize()}Javadoc", type: Javadoc) {
            description "Generates Javadoc for variant ${variant.name.capitalize()}"
           // println "Generates Javadoc task for variant ${variant.name.capitalize()}"

            source = variant.javaCompile.source
            options.links("http://docs.oracle.com/javase/6/docs/api/")
            options.links("http://d.android.com/reference/")

            //println 'classpath : ' + classpath.getFiles()
            //println 'options links : ' + options.links
            //println 'source : ' + source.getFiles()

            // First add all of your dependencies to the classpath, then add the android jars
            doFirst {
                //doFirst is needed else we get the error "Cannot create variant 'android-lint' after configuration" with gradle 4.4+
                classpath = files(variant.javaCompile.classpath.files, project.android.getBootClasspath())
            }
            classpath += files(android.getBootClasspath())


            // We're excluding these generated files
            exclude '**/BuildConfig.java'
            exclude '**/R.java'
            failOnError = false
            destinationDir = file("${project.docsDir}/javadoc")
            //destinationDir = file(System.getProperty("user.home") +"/pages")

        }

    }

}


//build javadoc
task javadoc(type: Javadoc) {
    failOnError false
    source = android.sourceSets.main.java.sourceFiles
    classpath += project.files(android.getBootClasspath().join(File.pathSeparator))
    classpath += configurations.compile
}

// build a jar with javadoc
task javadocJar(type: Jar, dependsOn: javadoc) {
    classifier = 'javadoc'
    from javadoc.destinationDir
}

//build sources jar
task androidSourcesJar(type: Jar) {
    classifier = 'sources'
    from android.sourceSets.main.java.sourceFiles
}

artifacts {
    archives androidSourcesJar, javadocJar
}

//sign if needed (based on the doSign variable)
if (project.getProperties().containsKey("doSign") && doSign=="true") {
    apply plugin: 'signing'

    ext."signing.keyId" = "0C140E36"
    //other properties are defined in the gradle.properties
    signing {
        sign configurations.archives
    }
}

/* from https://central.sonatype.org/pages/gradle.html */
uploadArchives  {
    repositories {
        //define local or remote upload repository (based on the doSign variable)
        mavenDeployer {
            if (project.getProperties().containsKey("doSign") && doSign=="true") {
                beforeDeployment { MavenDeployment deployment -> signing.signPom(deployment) }

                repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }

                snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                    authentication(userName: ossrhUsername, password: ossrhPassword)
                }
            }else {
                repository(url: mavenLocal().url)
            }

            pom.project {
                name project.description
                description project.description
                packaging "aar"

                organization("Eclipse Keyple"){
                    name "Eclipse Keyple"
                    url "https://projects.eclipse.org/projects/iot.keyple"
                }

                scm {
                    connection 'scm:git:git://github.com/eclipse/keyple-java.git'
                    developerConnection 'scm:git:https://github.com/eclipse/keyple-java.git'
                    url 'http://github.com/eclipse/keyple-java/tree/master'
                }

                licenses {
                    license {
                        name 'Eclipse Public License - v 2.0'
                        url 'https://www.eclipse.org/legal/epl-2.0/'
                    }
                }

                developers {
                    developer {
                        name 'Olivier Delcroix'
                        email 'odelcroi@gmail.com'
                    }
                    developer {
                        name 'Jean-Pierre Fortune'
                        email 'jean-pierre.fortune@ialto.com'
                    }
                    developer {
                        name 'Pierre Terr√©e'
                        email 'pierre.terree@calypsonet.org'
                    }
                }

                //dependencies mapping is defined manually
                pom.scopeMappings.with {
                    mappings.clear()
                    addMapping(1, configurations.implementation, 'compile')
                    //addMapping(2, configurations.compileOnly, 'runtime') openmobileapi is included in Android
                    addMapping(2, configurations.testImplementation, 'test')
                    addMapping(3, configurations.androidTestImplementation, 'test')

                }
            }
        }
    }
}



repositories {
    mavenLocal()
    maven {
        url 'https://repo.eclipse.org/service/local/repositories/maven_central/content'
    }
    //to import keyple snapshots
    maven {url 'https://oss.sonatype.org/content/repositories/snapshots' }
    //to import keyple releases
    maven { url 'https://oss.sonatype.org/content/repositories/releases' }
    //mavenCentral()
    google()
    jcenter()
}

dependencies {
    implementation fileTree(include: ['*.jar'], exclude: ['org.simalliance.openmobileapi.jar'], dir: 'libs')

    //keyple-core project
    implementation group: 'org.eclipse.keyple', name: 'keyple-java-core', version: '+'

    //android
    implementation "com.android.support:support-v4:${android_support_version}"
    implementation "com.android.support:appcompat-v7:${android_support_version}"

    //logging
    implementation "org.slf4j:slf4j-api:${slf4japi_version}"

    //omapi definition
    compileOnly files('libs/org.simalliance.openmobileapi.jar')
    /*
    Tests
     *

    /** Test **/
    testImplementation group: 'org.eclipse.keyple', name: 'keyple-java-calypso', version: project.version

    testImplementation files('libs/org.simalliance.openmobileapi.jar')

    /**Power mock**/
    androidTestImplementation "org.mockito:mockito-android:${mockito_android_version}"
    androidTestImplementation "org.powermock:powermock-api-mockito:${powermock_version}"
    androidTestImplementation "org.powermock:powermock-module-junit4:${powermock_version}"

    testImplementation "junit:junit:${junit_version}"
    testImplementation "org.powermock:powermock-api-mockito:${powermock_version}"
    testImplementation "org.powermock:powermock-module-junit4:${powermock_version}"
    /**End of power mock **/
}
